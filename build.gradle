import com.github.spotbugs.snom.SpotBugsReport

plugins {
    id 'java'
    id "com.github.spotbugs" version "6.0.9"
    id 'checkstyle'
    id 'jacoco'
    id 'application'
    id 'info.solidsoft.pitest' version '1.15.0'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

repositories {
    mavenCentral()
}

apply plugin : "java"
ext {
    javaMainClass = "presentation.RiskApp"
}

application {
    mainClassName = javaMainClass
}

sourceSets {
    main {
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation group: 'org.easymock', name: 'easymock', version: '4.2'
}

test {
    useJUnitPlatform()
}

spotbugsMain {
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

javafx {
    version = "17.0.10"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
    dependsOn test
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
}

test.finalizedBy(jacocoTestReport)

task codeCoverageInfo (dependsOn: jacocoTestReport) {
    ext.srcFile = file("${reportsDir}/jacoco/test/jacocoTestReport.xml")

    doLast {
        println "Transforming source file."

        def parser = new XmlParser()
        parser.setFeature("http://apache.org/xml/features/disallow-doctype-decl",
                false);
        parser.setFeature("http://apache.org/xml/features/nonvalidating/load-external-dtd", false);
        parser.setFeature("http://xml.org/sax/features/namespaces", false)

        def reportNode = parser.parse(srcFile)
        reportNode.children().each {
            if(it.name() == 'counter') {
                String type = it.@type
                float missed = it.@missed as float
                float covered = it.@covered as float
                float percentage = covered / (missed + covered) * 100
                println "Code Coverage[$type]: $percentage%"
            }
        }
    }
}

jacocoTestReport.finalizedBy(codeCoverageInfo)

build.dependsOn 'pitest'
check.dependsOn 'pitest'

pitest {
    targetClasses = ['domain.*', 'datasource.*']
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    junit5PluginVersion = '1.0.0'
}

group 'edu.rose-hulman.csse'
version '1.0'